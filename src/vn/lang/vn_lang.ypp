%require "3.2"

%language "c++"
%define api.namespace {YY}
%define api.parser.class {VNLangParser}
%define api.value.type variant
%define api.token.constructor
%define parse.error detailed
// Inadequacy Elimination LR
%define lr.type ielr
// Lookahead Correction
%define parse.lac full
%define parse.trace

%defines

%locations
%define api.location.file "vn_lang.location.hpp"

%code requires {
  // Appears high-ish in the generated .hpp file
  #include "vn_actions.hpp" // TODO we actually only need Item and Itemisation from here
  #include "node/tree_ptr.hpp"
  #include "tree/node_names.hpp"
  #include <string>
  namespace YY {
    class VNLangScanner;  
  	struct TokenMetadata
	{
		TreePtr<Node> as_designated;
		string as_ascii;
		wstring as_unicode;
		const AvailableNodeData::Block *as_andata_block;
	};     	
  };
  // Things we point to from semantic values
  namespace VN { 
    class Command;
    class Gnomon;
    class ResolverGnomon;
  }
}

%code { 
  // Appears high-ish in the generated .cpp file
  #include "vn_lang.lpp.hpp"  /* Generated by reflex. */
  #include <iostream>
  #include <fstream>
  #include "vn_commands.hpp"
  #undef yylex
  #define yylex lexer.lex  /* Within bison's parse() we should invoke lexer.lex(), not the global yylex() */
  
  // Strings that could be translated have _() around them. 
  // See https://www.gnu.org/software/gettext/manual/gettext.html
  #define _(String) (String)
  #define N_(String) String 
}

%parse-param { YY::VNLangScanner& lexer }
%parse-param { VN::VNLangActions *vn }
%parse-param { VN::VNLangRecogniser *recogniser }

%define api.token.prefix {TOK_}

%token
	DESIGNATE 								"‚™Æ"
	VN_SEMI 								"‚®ü"
	VN_COLON 								"‚¶Ç"
	VN_SEP 									"‚ö¨"
	VN_SUB_SEP								"üûÑ"
	'('
	')'
	';'
	RESOLUTION_OP 							"::"
	ENGINE	 								"Í©ú"
	STUFF		 							"‚©®"
	DELTA 									"‚ñ≤"
	DELTA_ARROW 							"‚Øà"
	BUILT_IN 								"‚ØÅ"
	OPEN_VN 								"„Äê"
	CLOSE_VN 								"„Äë"
	ANY 									"‚òÜ"
	STAR 									"‚òÖ"
	ID_BY_NAME								"‚äõ"
	SPECIFIC_ID								"üûä"
	BUILDER_ID								"‚ßá"
	OPEN_CHEVRON							"„Ää"
	CLOSE_CHEVRON							"„Äã"
	TRANSFORM								"‚§®"
	NEGATION								"¬¨"
	CONJUNCTION								"‚àß"
	DISJUNCTION								"‚à®"
	GRASS									"‚´¥"
	POINTER_IS								"‚Æé"
	BUILD_SIZE								"üÑΩ"
	STRINGIZE								"¬ß"
	PRERESTRICT								"‚ÄΩ"
	OPEN_TYPE								"‚¶Ö"
	CLOSE_TYPE								"‚¶Ü"
	OPEN_MAP_CALL							"„Äî"
	CLOSE_MAP_CALL							"„Äï"
	ATTRIBUTE								"‚Ä°"
	<string>	COMMA						_("comma")
	<string>	C_ASSIGN					"="
	<string>	C_COMPOUND_ASSIGN			_("C compound assignment operator")
	<string>	C_LOGICAL_OR_OP				"||"			
	<string>	C_LOGICAL_AND_OP			"&&"			
	<string>	C_BITWISE_OR_OP				"|"
	<string>	C_BITWISE_XOR_OP			"^"
	<string>	C_AMPERSAND					"&"
	<string>	C_EQUALITY_OP				_("C equality operator")
	<string>  	C_RELATIONAL_OP				_("C relation operator")
	<string>	C_SHIFT_OP					_("C shift operator")
	<string>	C_ADD_SUB_OP				_("C add/subtract")
	<string>    C_DIV_MOD_OP				_("C divide/modulo")
	<string> 	C_STAR						"*"
	<string>  	C_PREFIX_OP					_("C prefix operator")
	<string>	C_PRE_OR_INFIX_OP			_("C prefix/infix operator")
	<string>    C_PRE_OR_POSTFIX_OP 		_("C prefix/postfix operator")
	<wstring> 	STRING_LITERAL 				_("string")
	<string> 	INTEGRAL_LITERAL			_("integer literal")
	<bool>		BOOL_LITERAL				_("boolean literal")
	<string>	ASCII_FORMAT 				_("ASCII format string")
	<string>	TYPENAME					_("type name")
	<YY::TokenMetadata> TRANSFORM_NAME_UNIFIED	_("unified transformation name")
	<YY::TokenMetadata> TRANSFORM_NAME_NORMAL	_("normal transformation name")
	<YY::TokenMetadata> ASCII_NAME 				_("ASCII name")
	<YY::TokenMetadata> UNICODE_NAME 			_("Unicode name")
	<YY::TokenMetadata>	DESIGNATED_NORMAL		_("designated name")
	<YY::TokenMetadata>	DESIGNATED_TYPE			_("designated type name")
	<YY::TokenMetadata>	RESOLVED_NORMAL			_("resolved node name")
	<YY::TokenMetadata>	RESOLVED_TYPE			_("resolved type node name")
	<YY::TokenMetadata>	NODE_NAMESPACE			_("scope resolving name")
	<YY::TokenMetadata>	DECLARATION_OF			_("DeclarationOf")
	<YY::TokenMetadata>	TYPE_OF					_("TypeOf")
	<YY::TokenMetadata> NORM_TERM_KEYWORD		_("terminal keyword")
	<YY::TokenMetadata> PRIMITIVE_STMT_KEYWORD  _("statement keyword")
	<YY::TokenMetadata> SPACE_SEP_STMT_KEYWORD 	_("space-separated statement keyword")
	<YY::TokenMetadata> ARGS_BODY_STMT_KEYWORD 	_("args-and-body statement keyword")
	
%type <TreePtr<Node>> 				normal expr_primary expr_postfix expr_prefix expr_relational norm_disjunction norm_conjunction  
%type <TreePtr<Node>> 				expr_assignment expr_equality expr_add_etc expr_logical_or expr_logical_and 
%type <TreePtr<Node>> 				expr_mul_etc expr_shift expr_bitwise_and expr_bitwise_or expr_bitwise_xor  
%type <TreePtr<Node>> 				expr_comma_op expr_no_comma_op expr_expression expr_vn_prim expr_id_value
%type <TreePtr<Node>> 				type type_primary type_prefix type_disjunction type_conjunction type_vn_prim
%type <TreePtr<Node>> 				unified unified_prefix
%type <TreePtr<Node>> 				bare_statement statement_nc statement stmt_disjunction stmt_conjunction stmt_primary norm_expr_or_stmt
%type <TreePtr<Node>> 				compound comp_prefix
%type <wstring> 					vn_name
%type <string>						transform_norm_part transform_unif_part
%type <YY::TokenMetadata> 			resolved_name resolved_type_name node_namespace
%type <list<shared_ptr<VN::Command>>>			commands sep_commands
%type <shared_ptr<VN::Command>>		script_command 
%type <VN::Limit>					stuff_depth_limit
%type <VN::Itemisation>				itemisation items_bare items_sep
%type <VN::Item>					item
%type <shared_ptr<VN::Gnomon>>		part_resolve recogniser_command 
%type <shared_ptr<VN::Gnomon>>		start_transform_name_scope
%type <shared_ptr<VN::Gnomon>>		start_builtin_scope start_prerestrict_scope start_specific_id_scope start_id_by_name_scope start_build_id_scope
%type <list<TreePtr<Node>>>			statements
%type <list<TreePtr<Node>>>			comma_args semicolon_args
%type <const AvailableNodeData::Block *> builtin_name_part prerestrict_name_part specific_id_part id_by_name_part build_id_part
%type <const AvailableNodeData::Block *> type_builtin_name_part type_prerestrict_name_part type_specific_id_part type_id_by_name_part type_build_id_part
%%

vnscript 		: commands 								{ vn->OnVNScript($1); }
     
// This is to make the ‚®ü optional on last command
commands        : sep_commands							{ $$ = $1; }
				| sep_commands VN_SEMI					{ $$ = $1; }
				
// The ‚®ü turns patterns into commands. This rule must 
// be left-associative so calls to OnParse() happen
// in the correct order. STOP BREAKING THIS!!!
sep_commands 	: sep_commands VN_SEMI script_command		{ $$ = $1; $$.push_back($3); }	
				| script_command							{ $$.push_back($1); }
				| sep_commands VN_SEMI recogniser_command	{ $$ = $1; recogniser->AddGnomon($3); }	
				| recogniser_command						{ recogniser->AddGnomon($1); }
			
// Add more commands here
script_command	: ENGINE normal   		 		 	{ $$ = make_shared<VN::EngineCommand>($2, @1); }
				| normal 			  				{ $$ = make_shared<VN::PatternCommand>($1, @$); } // in trivial cases, this decays back to the node
				| ATTRIBUTE		  					{ $$ = make_shared<VN::AttributeCommand>(@$); }

// or here
recogniser_command	: vn_name DESIGNATE normal		{ $$ = make_shared<VN::NonTypeDesignationGnomon>($1, $3); }
					| vn_name DESIGNATE type		{ $$ = make_shared<VN::TypeDesignationGnomon>($1, $3); }


// This one dips into ASCII/C because VN doesn't otherwise need equality/inequality operators
stuff_depth_limit		: C_ASSIGN INTEGRAL_LITERAL					{ $$ = VN::Limit{ $1, @1, $2, @2 }; }

itemisation	: OPEN_VN items_bare CLOSE_VN  			{ $$ = $2; $$.loc = @$; }
			| OPEN_VN items_sep CLOSE_VN			{ $$ = $2; $$.loc = @$; $$.items.push_back({{}, @3}); }
			| OPEN_VN CLOSE_VN						{ $$.loc = @$; }
					
items_bare 	: items_sep item						{ $$ = $1; $$.items.push_back($2); }
			| item									{ $$.items.push_back($1); }

items_sep 	: items_bare VN_SEP		   				{ $$ = $1; } 
			| items_sep VN_SEP		 				{ $$ = $1; $$.items.push_back({{}, @2}); }
			| VN_SEP	 							{ $$.items.push_front({{}, @1}); }

item		: item VN_SUB_SEP unified				{ $$ = $1; $$.nodes.push_back($3); $$.loc = @$; }
			| unified								{ $$.nodes.push_back($1); $$.loc = @$; }

unified		: normal
			| type	

compound	: '{' VN_SEP '}'                   									{ $$ = vn->OnCompound({}, @2, {}, @3); } 
			| '{' statements VN_SEP '}'           								{ $$ = vn->OnCompound($2, @2, {}, @4); }
			| '{' VN_SEP statements '}'           								{ $$ = vn->OnCompound({}, @2, $3, @3); }
			| '{' statements VN_SEP statements '}'  							{ $$ = vn->OnCompound($2, @2, $4, @4); }

statements	: statement									{ $$.push_back($1); }
			| statements statement						{ $$ = $1; $$.push_back($2); }
         
statement	: stmt_disjunction

stmt_disjunction	: stmt_conjunction
					| stmt_disjunction DISJUNCTION stmt_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

stmt_conjunction 	: stmt_primary
					| stmt_conjunction CONJUNCTION stmt_primary					{ $$ = vn->OnConjunction($1, $3); }

stmt_primary: compound
			| statement_nc
			
statement_nc: bare_statement ';'												{ $$ = $1; }      
			| ARGS_BODY_STMT_KEYWORD '(' semicolon_args ')' stmt_primary		{ $$ = vn->OnArgsBodyStmt($1.as_ascii, @1, $3, @3, $5, @5); }

bare_statement		: expr_expression
					| expr_expression ':'										{ $$ = vn->OnLabel($1, @1); } 
					| PRIMITIVE_STMT_KEYWORD									{ $$ = vn->OnPrimitiveStmt($1.as_ascii, @1); }
					| SPACE_SEP_STMT_KEYWORD expr_expression					{ $$ = vn->OnSpaceSepStmt($1.as_ascii, @1, $2, @2); }

semicolon_args		: expr_expression											{ $$.push_back($1); }
					| semicolon_args ';' expr_expression						{ $$ = $1; $$.push_back($3); }

         
type		: type_disjunction          
          
type_disjunction	: type_conjunction
					| type_disjunction DISJUNCTION type_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

type_conjunction 	: type_prefix
					| type_conjunction CONJUNCTION type_prefix				{ $$ = vn->OnConjunction($1, $3); }
                    
type_prefix : type_primary
			| 				         DELTA type DELTA_ARROW type_prefix	{ $$ = vn->OnDelta($2, $4); }
			| 									      GRASS type_prefix	{ $$ = vn->OnGrass($2); }
			| 								     POINTER_IS type_prefix	{ $$ = vn->OnPointerIs($2); }
			| 								       NEGATION type_prefix	{ $$ = vn->OnNegation($2); }
			|                 type_prerestrict_name_part unified_prefix	{ $$ = vn->OnRestrict($1, @1, $2, @2); }

			                                        
type_primary: type_vn_prim
			| '(' type ')'														{ $$ = $2; }	

type_vn_prim: type_builtin_name_part itemisation								{ $$ = vn->OnBuiltIn($1, @1, $2); }
			| DESIGNATED_TYPE     									  			{ $$ = $1.as_designated; }		
   			| type_specific_id_part OPEN_VN STRING_LITERAL CLOSE_VN				{ $$ = vn->OnSpecificId($1, @1, $3, @3); }
			| type_specific_id_part OPEN_VN CLOSE_VN							{ $$ = vn->OnSpecificId($1, @1, wstring(), @3); }
			| type_id_by_name_part OPEN_VN STRING_LITERAL CLOSE_VN				{ $$ = vn->OnIdByName($1, @1, $3, @3); } // ASCII_NAME could be format
			| type_build_id_part OPEN_VN STRING_LITERAL VN_SEP CLOSE_VN 		{ $$ = vn->OnBuildId($1, @1, $3, @3, VN::Item{}); }
			| type_build_id_part OPEN_VN STRING_LITERAL VN_SEP item CLOSE_VN	{ $$ = vn->OnBuildId($1, @1, $3, @3, $5); }
			| type_build_id_part OPEN_VN VN_SEP item CLOSE_VN 					{ $$ = vn->OnBuildId($1, @1, wstring(), @3, $4); }

type_builtin_name_part : start_builtin_scope resolved_type_name				{ $$ = $2.as_andata_block; (void)$1; }
type_prerestrict_name_part : start_prerestrict_scope resolved_type_name		{ $$ = $2.as_andata_block; (void)$1; }

type_specific_id_part	: start_specific_id_scope resolved_type_name	{ $$ = $2.as_andata_block; (void)$1; }
type_id_by_name_part 	: start_id_by_name_scope resolved_type_name		{ $$ = $2.as_andata_block; (void)$1; }
type_build_id_part 		: start_build_id_scope resolved_type_name		{ $$ = $2.as_andata_block; (void)$1; }

normal				: norm_disjunction

norm_disjunction	: norm_conjunction
					| norm_disjunction DISJUNCTION norm_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

norm_conjunction 	: norm_expr_or_stmt
					| norm_conjunction CONJUNCTION norm_expr_or_stmt				{ $$ = vn->OnConjunction($1, $3); }
		
norm_expr_or_stmt	: expr_expression
					| statement_nc // statements not including Compound, which is expressional from this POV

expr_expression		: expr_comma_op
                                   
expr_comma_op       : expr_no_comma_op
                    | expr_comma_op COMMA expr_no_comma_op						{ $$ = vn->OnInfixOperator($2, $1, $3); }
   
comma_args			: expr_no_comma_op											{ $$.push_back($1); }
					| comma_args COMMA expr_no_comma_op							{ $$ = $1; $$.push_back($3); }			

expr_no_comma_op 	: expr_id_value

expr_id_value		: expr_assignment
					| expr_id_value VN_COLON expr_assignment					{ $$ = vn->OnIdValuePair($1, @1, $3); }
					
expr_assignment 	: expr_logical_or 
					| comp_prefix // Branch off here for compound to avoid conflicts with prefix = (used for inits)
					| 				   expr_logical_or C_ASSIGN expr_assignment	{ $$ = vn->OnInfixOperator($2, $1, $3); }
					|         expr_logical_or C_COMPOUND_ASSIGN expr_assignment	{ $$ = vn->OnInfixOperator($2, $1, $3); }
					|   expr_logical_or '?' expr_expression ':' expr_assignment	{ $$ = vn->OnConditionalOperator($1, $3, $5); }

expr_logical_or 	: expr_logical_and
					| expr_logical_or C_LOGICAL_OR_OP expr_logical_and			{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
expr_logical_and	: expr_bitwise_or
					| expr_logical_and C_LOGICAL_AND_OP expr_bitwise_or			{ $$ = vn->OnInfixOperator($2, $1, $3); }

expr_bitwise_or 	: expr_bitwise_xor
					| expr_bitwise_or C_BITWISE_OR_OP expr_bitwise_xor			{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
expr_bitwise_xor	: expr_bitwise_and
					| expr_bitwise_xor C_BITWISE_XOR_OP expr_bitwise_and		{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
expr_bitwise_and	: expr_equality
					| expr_bitwise_and C_AMPERSAND expr_equality				{ $$ = vn->OnInfixOperator($2, $1, $3); }

expr_equality 		: expr_relational
					| expr_equality C_EQUALITY_OP expr_relational				{ $$ = vn->OnInfixOperator($2, $1, $3); }

expr_relational 	: expr_shift
					| expr_relational C_RELATIONAL_OP expr_shift				{ $$ = vn->OnInfixOperator($2, $1, $3); }

expr_shift			: expr_add_etc
					| expr_shift C_SHIFT_OP expr_add_etc						{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
expr_add_etc 		: expr_mul_etc
					| expr_add_etc C_ADD_SUB_OP expr_mul_etc					{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
expr_mul_etc 		: expr_prefix
					| expr_mul_etc C_DIV_MOD_OP expr_prefix						{ $$ = vn->OnInfixOperator($2, $1, $3); }
					| expr_mul_etc '*' expr_prefix								{ $$ = vn->OnInfixOperator("*", $1, $3); }
		
expr_prefix : expr_postfix
			| 					    C_PREFIX_OP 		expr_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_AMPERSAND 		expr_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_STAR		 		expr_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_ADD_SUB_OP	    expr_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_PRE_OR_INFIX_OP   expr_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_PRE_OR_POSTFIX_OP expr_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 									  GRASS expr_prefix	 { $$ = vn->OnGrass($2); }
			| 								 POINTER_IS expr_prefix	 { $$ = vn->OnPointerIs($2); }
			| 			       DELTA normal DELTA_ARROW expr_prefix	 { $$ = vn->OnDelta($2, $4); }
			| 								   NEGATION expr_prefix	 { $$ = vn->OnNegation($2); }
			|       				transform_norm_part type_prefix	 { $$ = vn->OnTransform($1, @1, $2, @2); }
			| 								 BUILD_SIZE unified_prefix	{ $$ = vn->OnBuildSize($2); }
			| 								  STRINGIZE unified_prefix	{ $$ = vn->OnStringize($2); }
			|  									  STUFF unified_prefix	{ $$ = vn->OnStuff($2, nullptr, VN::Limit()); }
			|  STUFF OPEN_VN           unified CLOSE_VN unified_prefix	{ $$ = vn->OnStuff($5, $3, VN::Limit()); }
			|  STUFF OPEN_VN stuff_depth_limit CLOSE_VN unified_prefix	{ $$ = vn->OnStuff($5, nullptr, $3); }
			|                     prerestrict_name_part unified_prefix	{ $$ = vn->OnRestrict($1, @1, $2, @2); }

transform_norm_part : start_transform_name_scope TRANSFORM_NAME_NORMAL { $$ = $2.as_ascii; (void)$1; }

// Use a prerestriction to disambiguate ‚§®„ÄêDeclarationOf„Äë...
unified_prefix	: expr_prefix
				| type_prefix
				| transform_unif_part expr_prefix	 			{ $$ = vn->OnTransform($1, @1, $2, @2); }
  
transform_unif_part : start_transform_name_scope TRANSFORM_NAME_UNIFIED { $$ = $2.as_ascii; (void)$1; }
  
expr_postfix: expr_primary     
			| expr_postfix C_PRE_OR_POSTFIX_OP 				 		{ $$ = vn->OnPostfixOperator($2, $1); }
			| expr_postfix '[' normal ']'	 				 		{ $$ = vn->OnSubscript($1, $3); }
			| expr_postfix OPEN_MAP_CALL comma_args CLOSE_MAP_CALL	{ $$ = vn->OnMapArgsCall($1, $3); }
			| expr_postfix OPEN_MAP_CALL CLOSE_MAP_CALL				{ $$ = vn->OnMapArgsCall($1, list<TreePtr<Node>>()); }
			| expr_postfix '(' comma_args ')'		 				{ $$ = vn->OnSeqArgsCall($1, $3); }
			| expr_postfix '(' ')'	 								{ $$ = vn->OnSeqArgsCall($1, list<TreePtr<Node>>()); }
			| expr_postfix '.' expr_primary							{ $$ = vn->OnLookup($1, $3, @3); }
		
expr_primary: expr_vn_prim
			| '(' commands ')'                          		{ $$ = vn->OnEmbeddedCommands($2); }
			| STAR												{ $$ = vn->OnStar(nullptr); }
			| STAR OPEN_VN normal CLOSE_VN						{ $$ = vn->OnStar($3); }
			| INTEGRAL_LITERAL					      			{ $$ = vn->OnIntegralLiteral($1, @1); }
			| STRING_LITERAL					      			{ $$ = vn->OnStringLiteral($1); }
			| BOOL_LITERAL										{ $$ = vn->OnBoolLiteral($1); }
			| ANY												{ $$ = nullptr; }
			| NORM_TERM_KEYWORD									{ $$ = vn->OnNormalTerminalKeyword($1.as_ascii, @1); }
			| C_ASSIGN '{' '}'             				  		{ $$ = vn->OnArrayLiteral({}); } // ={...} dismbiguates against compound
			| C_ASSIGN '{' comma_args '}'      		  	  		{ $$ = vn->OnArrayLiteral($3); }               

expr_vn_prim: builtin_name_part itemisation								{ $$ = vn->OnBuiltIn($1, @1, $2); }
			| DESIGNATED_NORMAL       				  					{ $$ = $1.as_designated; }
			| specific_id_part OPEN_VN STRING_LITERAL CLOSE_VN			{ $$ = vn->OnSpecificId($1, @1, $3, @3); }
			| specific_id_part OPEN_VN CLOSE_VN							{ $$ = vn->OnSpecificId($1, @1, wstring(), @3); }
			| id_by_name_part OPEN_VN STRING_LITERAL CLOSE_VN			{ $$ = vn->OnIdByName($1, @1, $3, @3); } // ASCII_NAME could be format
			| build_id_part OPEN_VN STRING_LITERAL VN_SEP CLOSE_VN 		{ $$ = vn->OnBuildId($1, @1, $3, @3, VN::Item{}); }
			| build_id_part OPEN_VN STRING_LITERAL VN_SEP item CLOSE_VN	{ $$ = vn->OnBuildId($1, @1, $3, @3, $5); }
			| build_id_part OPEN_VN VN_SEP item CLOSE_VN 				{ $$ = vn->OnBuildId($1, @1, wstring(), @3, $4); }

builtin_name_part 	: start_builtin_scope resolved_name			{ $$ = $2.as_andata_block; (void)$1; }
prerestrict_name_part : start_prerestrict_scope resolved_name	{ $$ = $2.as_andata_block; (void)$1; }

specific_id_part	: start_specific_id_scope resolved_name	{ $$ = $2.as_andata_block; (void)$1; }
id_by_name_part 	: start_id_by_name_scope resolved_name	{ $$ = $2.as_andata_block; (void)$1; }
build_id_part 		: start_build_id_scope resolved_name	{ $$ = $2.as_andata_block; (void)$1; }


// Because there's no separator in {{code} expr} the prefix ops would conflict with infix that use same symbol,
// so we'll need to complete a branch of the syntax for compounds with a copy of the unary VN operators (none are postfix).
comp_prefix : compound 
			| 			       DELTA normal DELTA_ARROW comp_prefix	{ $$ = vn->OnDelta($2, $4); }
			| 								   NEGATION comp_prefix	{ $$ = vn->OnNegation($2); }
			|       				transform_norm_part comp_prefix	{ $$ = vn->OnTransform($1, @1, $2, @2); }
			| 								 BUILD_SIZE comp_prefix	{ $$ = vn->OnBuildSize($2); }
			| 								  STRINGIZE comp_prefix	{ $$ = vn->OnStringize($2); }
			|  									  STUFF comp_prefix	{ $$ = vn->OnStuff($2, nullptr, VN::Limit()); }
			|  STUFF OPEN_VN           unified CLOSE_VN comp_prefix	{ $$ = vn->OnStuff($5, $3, VN::Limit()); }
			|  STUFF OPEN_VN stuff_depth_limit CLOSE_VN comp_prefix	{ $$ = vn->OnStuff($5, nullptr, $3); }
			|                     prerestrict_name_part comp_prefix	{ $$ = vn->OnRestrict($1, @1, $2, @2); }


vn_name		: UNICODE_NAME    							{ $$ = $1.as_unicode; }
			| ASCII_NAME								{ $$ = $1.as_unicode; }     
          
// RESOLVED_X is strong i.e. there was definitely a ::. resolved_x is weak and
// doesn't require this. See comment by part_resolve for why we don't need a value from 
// part_resolve. The Gnomon is still around because we *could* use $1 here, so it must be valid.
resolved_name	: part_resolve RESOLVED_NORMAL			{ $$ = $2; }
				| RESOLVED_NORMAL						{ $$ = $1; }

resolved_type_name	: part_resolve RESOLVED_TYPE		{ $$ = $2; }
					| RESOLVED_TYPE						{ $$ = $1; }

// On sight of ::, we inform the recognition layer, which returns a shared_ptr which will 
// expire when the production is complete or an error forces unwind. The naming layer
// keeps weak_ptrs to these and will send us RESOLVED_NORMAL if one exists.
part_resolve	: node_namespace RESOLUTION_OP			{ $$ = make_shared<VN::ResolverGnomon>($1.as_andata_block);
                                                          recogniser->AddGnomon($$); }
    
node_namespace 	: part_resolve NODE_NAMESPACE			{ $$ = $2; }
				| NODE_NAMESPACE						{ $$ = $1; }				      

start_builtin_scope		: BUILT_IN 			{ $$ = make_shared<VN::NodeNameScopeGnomon>(); 
										      recogniser->AddGnomon($$); }
										      
start_prerestrict_scope	: PRERESTRICT		{ $$ = make_shared<VN::NodeNameScopeGnomon>(); 
										      recogniser->AddGnomon($$); }

start_specific_id_scope	: SPECIFIC_ID 		{ $$ = make_shared<VN::IdentifierDiscriminatorScopeGnomon>(); 
											  recogniser->AddGnomon($$); }

start_id_by_name_scope	: ID_BY_NAME 		{ $$ = make_shared<VN::IdentifierDiscriminatorScopeGnomon>(); 
											  recogniser->AddGnomon($$); }

start_build_id_scope	: BUILDER_ID 		{ $$ = make_shared<VN::IdentifierDiscriminatorScopeGnomon>(); 
											  recogniser->AddGnomon($$); }

start_transform_name_scope	: TRANSFORM		{ $$ = make_shared<VN::TransformNameScopeGnomon>(); 
											  recogniser->AddGnomon($$); }

%%
