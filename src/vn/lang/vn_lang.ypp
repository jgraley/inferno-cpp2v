%require "3.2"

%language "c++"
%define api.namespace {YY}
%define api.parser.class {VNLangParser}
%define api.value.type variant
%define api.token.constructor
%define parse.error detailed
%define parse.lac full

%defines

%locations
%define api.location.file "vn_lang.location.hpp"

%code requires {
  namespace YY {
    class VNLangScanner;
  };
  #include "vn_parse.hpp"
  #include "vn_commands.hpp"
}

%code { 
  #include "vn_lang.lpp.hpp"  /* Generated by reflex. */
  #include <iostream>
  #include <fstream>
  #undef yylex
  #define yylex lexer.lex  /* Within bison's parse() we should invoke lexer.lex(), not the global yylex() */
  
  // Strings that could be translated have _() around them. 
  // See https://www.gnu.org/software/gettext/manual/gettext.html
  #define _(String) (String)
  #define N_(String) String
}

%parse-param { YY::VNLangScanner& lexer }
%parse-param { VN::VNParse *vn }

%define api.token.prefix {TOK_}

// The order seems to matter here, find out why TODO
%token
	DESIGNATE 								"‚™Æ"
	VN_SEMI 								"‚®ü"
	VN_SEP 									"‚ö¨"
	'+'
	'-'
	'*'
	'/'
	'='
	'('
	')'
	';'
	SCOPE_RES 								"::"
	ENGINE	 								"Í©ú"
	STUFF		 							"‚©®"
	DELTA 									"‚ñ≤"
	DELTA_ARROW 							"‚Øà"
	BUILT_IN 								"‚ØÅ"
	OPEN_RESTRICT 							"„Äê"
	CLOSE_RESTRICT 							"„Äë"
	ANY 									"‚òÜ"
	STAR 									"‚òÖ"
	ID_BY_NAME								"‚äõ"
	BUILD_ID								"‚ßá"
	OPEN_CHEVRON							"„Ää"
	CLOSE_CHEVRON							"„Äã"
	TRANSFORM								"‚§®"
	NEGATION								"¬¨"
	CONJUNCTION								"‚àß"
	DISJUNCTION								"‚à®"
	GRASS									"‚´¥"
	POINTER_IS								"‚Æé"
	BUILD_SIZE								"üÑΩ"
	STRINGIZE								"¬ß"
	<wstring> 	STRING_LITERAL 				_("string")
	<unsigned> 	NUMERIC_LITERAL				_("number")
	<bool>		BOOL_LITERAL				_("boolean literal")
	<string>  	ASCII_NAME 					_("ASCII name")
	<string>  	ASCII_FORMAT 				_("ASCII format string")
	<wstring> 	UNICODE_NAME 				_("Unicode name")

%left DISJUNCTION
%left CONJUNCTION
%right '='
%left  '+' '-'
%left  '*' '/'
%precedence PREFIX ENGINE STUFF DELTA DELTA_ARROW TRANSFORM NEGATION GRASS POINTER_IS BUILD_SIZE STRINGIZE
%precedence OPEN_CHEVRON CLOSE_CHEVRON OPEN_RESTRICT CLOSE_RESTRICT
%precedence BUILT_IN BOOL_LITERAL STRING_LITERAL NUMERIC_LITERAL

%type <TreePtr<Node>> 				pattern
%type <wstring> 					vn_name
%type <string>						format
%type <list<string>> 				scoped_name
%type <VN::Command::List>			commands
%type <VN::Command::List>			sep_commands
%type <shared_ptr<VN::Command>>		bare_command
%type <VN::Limit>					limit
%type <VN::Itemisation>				itemisation
%type <VN::Itemisation>				items_bare
%type <VN::Itemisation>				items_sep
%type <VN::Item>					item

%%

vnscript 		: commands 						{ vn->OnVNScript($1); }
     
// This is to make the ‚®ü optional on last command
commands        : sep_commands							{ $$ = $1; }
				| sep_commands VN_SEMI					{ $$ = $1; }
				
// The ‚®ü turns patterns into commands. This rule must 
// be left-associative so calls to OnParse() happen
// in the correct order. STOP BREAKING THIS!!!
sep_commands 	: sep_commands VN_SEMI bare_command		{ $$ = $1; if( $3->OnParse(vn) ) $$.push_back($3); }	
				| bare_command							{ if( $1->OnParse(vn) ) $$.push_back($1); }
			
// Add more commands here
bare_command	: vn_name DESIGNATE pattern				{ $$ = make_shared<VN::Designation>($1, $3, @2); }
				| ENGINE pattern   		 			 	{ $$ = make_shared<VN::EngineCommand>($2, @1); }
				| pattern 			  					{ $$ = make_shared<VN::PatternCommand>($1, @$); }
        
pattern	: pattern '+' pattern      { $$ = $1; }
		| pattern '-' pattern      { $$ = $1; }
		| pattern '*' pattern      { $$ = $1; }
		| pattern '/' pattern      { $$ = $1; }
		| STAR												{ $$ = vn->OnStar(nullptr); }
		| STAR OPEN_CHEVRON pattern CLOSE_CHEVRON			{ $$ = vn->OnStar($3); }
		| STUFF pattern 		     					 	{ $$ = vn->OnStuff($2, nullptr, VN::Limit()); }
		| STUFF OPEN_CHEVRON pattern CLOSE_CHEVRON pattern 	{ $$ = vn->OnStuff($5, $3, VN::Limit()); }
		| STUFF OPEN_CHEVRON limit CLOSE_CHEVRON pattern 	{ $$ = vn->OnStuff($5, nullptr, $3); }
		| DELTA pattern DELTA_ARROW pattern   		   		{ $$ = vn->OnDelta($2, $4); }
		| BUILT_IN scoped_name itemisation					{ $$ = vn->OnBuiltIn($2, @2, $3); }
		| BUILT_IN scoped_name								{ $$ = vn->OnBuiltIn($2, @2, VN::Itemisation{{}, @2}); }
		| '-' %prec PREFIX pattern                 			{ $$ = vn->OnPrefixOperator("-", $2); }
		| '(' commands ')'                          		{ $$ = vn->OnEmbeddedCommands($2); }
		| OPEN_RESTRICT scoped_name CLOSE_RESTRICT pattern 	{ $$ = vn->OnRestrict($2, @2, $4, @4); }
		| vn_name       						  			{ $$ = vn->OnName($1, @1); }
		| NUMERIC_LITERAL					      			{ $$ = vn->OnNumericLiteral($1); }
		| STRING_LITERAL					      			{ $$ = vn->OnStringLiteral($1); }
		| BOOL_LITERAL										{ $$ = vn->OnBoolLiteral($1); }
		| ANY												{ $$ = nullptr; }
		| ID_BY_NAME OPEN_CHEVRON scoped_name ',' ASCII_NAME CLOSE_CHEVRON		{ $$ = vn->OnIdByName($3, @3, $5, @5); } // ASCII_NAME could be format
		| BUILD_ID OPEN_CHEVRON scoped_name ',' format CLOSE_CHEVRON 			{ $$ = vn->OnBuildId($3, @3, $5, @5, VN::Item{}); }
		| BUILD_ID OPEN_CHEVRON scoped_name ',' format ',' item CLOSE_CHEVRON 	{ $$ = vn->OnBuildId($3, @3, $5, @5, $7); }
		| BUILD_ID OPEN_CHEVRON scoped_name ',' ',' item CLOSE_CHEVRON 			{ $$ = vn->OnBuildId($3, @3, "", @5, $6); }
		| TRANSFORM OPEN_CHEVRON ASCII_NAME CLOSE_CHEVRON pattern				{ $$ = vn->OnTransform($3, @3, $5, @5); }
		| NEGATION pattern 		     					 	{ $$ = vn->OnNegation($2); }
		| pattern CONJUNCTION pattern						{ $$ = vn->OnConjunction($1, $3); }
		| pattern DISJUNCTION pattern						{ $$ = vn->OnDisjunction($1, $3); }
		| GRASS pattern										{ $$ = vn->OnGrass($2); }
		| POINTER_IS pattern								{ $$ = vn->OnPointerIs($2); }
		| BUILD_SIZE pattern								{ $$ = vn->OnBuildSize($2); }
		| STRINGIZE pattern									{ $$ = vn->OnStringize($2); }
		;		

// This one dips into ASCII/C because VN doesn't otherwise need inequalities.
limit		: '=' NUMERIC_LITERAL						{ $$ = VN::Limit{ "=", @1, $2, @2 }; }

itemisation : '(' items_bare ')'    					{ $$ = $2; $$.loc = @$; }
			| '(' items_sep ')'	    					{ $$ = $2; $$.loc = @$; $$.items.push_back({{}, @3}); }
			| '(' ')'									{ $$.loc = @$; $$.items.push_back({{}, @2}); }
  
items_bare 	: items_sep item							{ $$ = $1; $$.items.push_back($2); }
			| item										{ $$.items.push_back($1); }

items_sep 	: items_bare VN_SEP		   					{ $$ = $1; } 
			| items_sep VN_SEP		 					{ $$ = $1; $$.items.push_back({{}, @2}); }
			| VN_SEP	 								{ $$.items.push_front({{}, @1}); }

item		: item ',' pattern    						{ $$ = $1; $$.nodes.push_back($3); $$.loc = @$; }
			| pattern									{ $$.nodes.push_back($1); $$.loc = @$; }

format		: ASCII_FORMAT  							{ $$ = $1; }
			| ASCII_NAME		  						{ $$ = $1; }

vn_name			: UNICODE_NAME    						{ $$ = $1; }
				| ASCII_NAME							{ $$ = ToUnicode($1); }     
     
scoped_name		: ASCII_NAME SCOPE_RES scoped_name		{ $$ = $3; $$.push_front($1); }
				| ASCII_NAME							{ $$.push_front($1); }

%%
