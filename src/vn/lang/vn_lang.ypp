%require "3.2"

%language "c++"
%define api.namespace {YY}
%define api.parser.class {VNLangParser}
%define api.value.type variant
%define api.token.constructor
%define parse.error detailed
// Inadequacy Elimination LR
%define lr.type ielr
// Lookahead Correction
%define parse.lac full
%define parse.trace

%defines

%locations
%define api.location.file "vn_lang.location.hpp"

%code requires {
  // Appears high-ish in the generated .hpp file
  #include "vn_parse.hpp" // TODO we actually only need Item and Itemisation from here
  #include "node/tree_ptr.hpp"
  #include "tree/node_names.hpp"
  #include <string>
  namespace YY {
    class VNLangScanner;  
  	struct TokenMetadata
	{
		TreePtr<Node> as_designated;
		string as_ascii;
		wstring as_unicode;
		const AvailableNodeData::Block *as_andata_block;
	};     	
  };
  // Things we point to from semantic values
  namespace VN { 
    class Command;
    class Gnomon;
    class ResolverGnomon;
  }
}

%code { 
  // Appears high-ish in the generated .cpp file
  #include "vn_lang.lpp.hpp"  /* Generated by reflex. */
  #include <iostream>
  #include <fstream>
  #include "vn_commands.hpp"
  #undef yylex
  #define yylex lexer.lex  /* Within bison's parse() we should invoke lexer.lex(), not the global yylex() */
  
  // Strings that could be translated have _() around them. 
  // See https://www.gnu.org/software/gettext/manual/gettext.html
  #define _(String) (String)
  #define N_(String) String 
}

%parse-param { YY::VNLangScanner& lexer }
%parse-param { VN::VNParse *vn }
%parse-param { VN::VNLangRecogniser *recogniser }

%define api.token.prefix {TOK_}

%token
	DESIGNATE 								"‚™Æ"
	VN_SEMI 								"‚®ü"
	VN_COLON 								"‚¶Ç"
	VN_SEP 									"‚ö¨"
	'('
	')'
	';'
	RESOLUTION_OP 							"::"
	ENGINE	 								"Í©ú"
	STUFF		 							"‚©®"
	DELTA 									"‚ñ≤"
	DELTA_ARROW 							"‚Øà"
	BUILT_IN 								"‚ØÅ"
	OPEN_VN 								"„Äê"
	CLOSE_VN 								"„Äë"
	ANY 									"‚òÜ"
	STAR 									"‚òÖ"
	ID_BY_NAME								"‚äõ"
	SPECIFIC_ID								"üûä"
	BUILDER_ID								"‚ßá"
	OPEN_CHEVRON							"„Ää"
	CLOSE_CHEVRON							"„Äã"
	TRANSFORM								"‚§®"
	NEGATION								"¬¨"
	CONJUNCTION								"‚àß"
	DISJUNCTION								"‚à®"
	GRASS									"‚´¥"
	POINTER_IS								"‚Æé"
	BUILD_SIZE								"üÑΩ"
	STRINGIZE								"¬ß"
	PRERESTRICT								"‚ÄΩ"
	OPEN_TYPE								"‚¶Ö"
	CLOSE_TYPE								"‚¶Ü"
	OPEN_MAP_CALL							"„Äî"
	CLOSE_MAP_CALL							"„Äï"
	ATTRIBUTE								"‚Ä°"
	<string>	COMMA						_("comma")
	<string>	C_ASSIGN					"="
	<string>	C_COMPOUND_ASSIGN			_("C compound assignment operator")
	<string>	C_LOGICAL_OR_OP				"||"			
	<string>	C_LOGICAL_AND_OP			"&&"			
	<string>	C_BITWISE_OR_OP				"|"
	<string>	C_BITWISE_XOR_OP			"^"
	<string>	C_AMPERSAND					"&"
	<string>	C_EQUALITY_OP				_("C equality operator")
	<string>  	C_RELATIONAL_OP				_("C relation operator")
	<string>	C_SHIFT_OP					_("C shift operator")
	<string>	C_ADD_SUB_OP				_("C add/subtract")
	<string>    C_DIV_MOD_OP				_("C divide/modulo")
	<string> 	C_STAR						"*"
	<string>  	C_PREFIX_OP					_("C prefix operator")
	<string>	C_PRE_OR_INFIX_OP			_("C prefix/infix operator")
	<string>    C_PRE_OR_POSTFIX_OP 		_("C prefix/postfix operator")
	<wstring> 	STRING_LITERAL 				_("string")
	<string> 	INTEGRAL_LITERAL			_("integer literal")
	<bool>		BOOL_LITERAL				_("boolean literal")
	<string>	ASCII_FORMAT 				_("ASCII format string")
	<string>	TYPENAME					_("type name")
	<YY::TokenMetadata> TRANSFORM_NAME_UNIFIED		_("unified transformation name")
	<YY::TokenMetadata> TRANSFORM_NAME_NORMAL		_("normal transformation name")
	<YY::TokenMetadata> ASCII_NAME 				_("ASCII name")
	<YY::TokenMetadata> UNICODE_NAME 			_("Unicode name")
	<YY::TokenMetadata>	DESIGNATED_NORMAL		_("designated name")
	<YY::TokenMetadata>	DESIGNATED_TYPE			_("designated type name")
	<YY::TokenMetadata>	RESOLVED_NORMAL			_("resolved node name")
	<YY::TokenMetadata>	RESOLVED_TYPE			_("resolved type node name")
	<YY::TokenMetadata>	NODE_NAMESPACE			_("scope resolving name")
	<YY::TokenMetadata>	DECLARATION_OF			_("DeclarationOf")
	<YY::TokenMetadata>	TYPE_OF					_("TypeOf")
	<YY::TokenMetadata> NORM_TERM_KEYWORD		_("terminal keyword")
	<YY::TokenMetadata> SPACE_SEP_STMT_KEYWORD 	_("space-separated statement keyword")
	
%type <TreePtr<Node>> 				normal norm_unit norm_postfix norm_prefix norm_relational norm_disjunction norm_conjunction  
%type <TreePtr<Node>> 				norm_assignment norm_equality norm_add_etc norm_logical_or norm_logical_and 
%type <TreePtr<Node>> 				norm_mul_etc norm_shift norm_bitwise_and norm_bitwise_or norm_bitwise_xor  
%type <TreePtr<Node>> 				norm_paren norm_expression
%type <TreePtr<Node>> 				type type_unit type_prefix type_disjunction type_conjunction type_paren
%type <TreePtr<Node>> 				unified unified_prefix
%type <TreePtr<Node>> 				bare_statement
%type <TreePtr<Node>> 				norm_id_value
%type <wstring> 					vn_name
%type <string>						transform_norm_part transform_unif_part
%type <YY::TokenMetadata> 			resolved_name resolved_type_name node_namespace
%type <list<shared_ptr<VN::Command>>>			commands sep_commands
%type <shared_ptr<VN::Command>>		script_command 
%type <VN::Limit>					stuff_depth_limit
%type <VN::Itemisation>				close_itemisation items_bare items_sep
%type <VN::Item>					item
%type <shared_ptr<VN::Gnomon>>		part_resolve recogniser_command 
%type <shared_ptr<VN::Gnomon>>		start_node_name_scope start_id_discrim_scope start_transform_name_scope
%type <list<TreePtr<Node>>>			bare_statements
%type <list<TreePtr<Node>>>			arguments
%type <const AvailableNodeData::Block *> node_name_part type_node_name_part id_discrim_part
%type <const AvailableNodeData::Block *> type_id_discrim_part 
%%

vnscript 		: commands 								{ vn->OnVNScript($1); }
     
// This is to make the ‚®ü optional on last command
commands        : sep_commands							{ $$ = $1; }
				| sep_commands VN_SEMI					{ $$ = $1; }
				
// The ‚®ü turns patterns into commands. This rule must 
// be left-associative so calls to OnParse() happen
// in the correct order. STOP BREAKING THIS!!!
sep_commands 	: sep_commands VN_SEMI script_command		{ $$ = $1; $$.push_back($3); }	
				| script_command							{ $$.push_back($1); }
				| sep_commands VN_SEMI recogniser_command	{ $$ = $1; recogniser->AddGnomon($3); }	
				| recogniser_command						{ recogniser->AddGnomon($1); }
			
// Add more commands here
script_command	: ENGINE normal   		 		 	{ $$ = make_shared<VN::EngineCommand>($2, @1); }
				| normal 			  				{ $$ = make_shared<VN::PatternCommand>($1, @$); } // in trivial cases, this decays back to the node
				| ATTRIBUTE		  					{ $$ = make_shared<VN::AttributeCommand>(@$); }

// or here
recogniser_command	: vn_name DESIGNATE normal		{ $$ = make_shared<VN::NonTypeDesignationGnomon>($1, $3); }
					| vn_name DESIGNATE type		{ $$ = make_shared<VN::TypeDesignationGnomon>($1, $3); }


// This one dips into ASCII/C because VN doesn't otherwise need equality/inequality operators
stuff_depth_limit		: C_ASSIGN INTEGRAL_LITERAL					{ $$ = VN::Limit{ $1, @1, $2, @2 }; }

close_itemisation	: VN_SEP items_bare CLOSE_VN  	{ $$ = $2; $$.loc = @$; }
					| VN_SEP items_sep CLOSE_VN		{ $$ = $2; $$.loc = @$; $$.items.push_back({{}, @3}); }
					| CLOSE_VN						{ $$.loc = @$; }
					
items_bare 	: items_sep item						{ $$ = $1; $$.items.push_back($2); }
			| item									{ $$.items.push_back($1); }

items_sep 	: items_bare VN_SEP		   				{ $$ = $1; } 
			| items_sep VN_SEP		 				{ $$ = $1; $$.items.push_back({{}, @2}); }
			| VN_SEP	 							{ $$.items.push_front({{}, @1}); }

item		: item COMMA unified					{ $$ = $1; $$.nodes.push_back($3); $$.loc = @$; }
			| unified								{ $$.nodes.push_back($1); $$.loc = @$; }

unified		: normal								{ $$ = $1; }
			| type									{ $$ = $1; }

bare_statements	: bare_statement 						{ $$.push_back($1); }
				| bare_statements ';' bare_statement 	{ $$ = $1; $$.push_back($3); }
         
type			: type_disjunction          
          
type_disjunction	: type_conjunction
					| type_disjunction DISJUNCTION type_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

type_conjunction 	: type_prefix
					| type_conjunction CONJUNCTION type_prefix				{ $$ = vn->OnConjunction($1, $3); }
                    
type_prefix : type_paren
			| 				         DELTA type DELTA_ARROW type_prefix	{ $$ = vn->OnDelta($2, $4); }
			| 									      GRASS type_prefix	{ $$ = vn->OnGrass($2); }
			| 								     POINTER_IS type_prefix	{ $$ = vn->OnPointerIs($2); }
			| 								       NEGATION type_prefix	{ $$ = vn->OnNegation($2); }
			|   PRERESTRICT type_node_name_part CLOSE_VN unified_prefix	{ $$ = vn->OnRestrict($2, @2, $4, @4); }

type_paren  : type_unit
			| '(' type ')'									{ $$ = $2; }	
			                                        
type_unit	: BUILT_IN type_node_name_part close_itemisation								{ $$ = vn->OnBuiltIn($2, @2, $3); }
			| DESIGNATED_TYPE     									  				 		{ $$ = $1.as_designated; }		
   			| SPECIFIC_ID type_id_discrim_part VN_SEP STRING_LITERAL CLOSE_VN				{ $$ = vn->OnSpecificId($2, @2, $4, @4); }
			| SPECIFIC_ID type_id_discrim_part VN_SEP CLOSE_VN								{ $$ = vn->OnSpecificId($2, @2, wstring(), @4); }
			| ID_BY_NAME type_id_discrim_part VN_SEP STRING_LITERAL CLOSE_VN				{ $$ = vn->OnIdByName($2, @2, $4, @4); } // ASCII_NAME could be format
			| BUILDER_ID type_id_discrim_part VN_SEP STRING_LITERAL VN_SEP CLOSE_VN 		{ $$ = vn->OnBuildId($2, @2, $4, @4, VN::Item{}); }
			| BUILDER_ID type_id_discrim_part VN_SEP STRING_LITERAL VN_SEP item CLOSE_VN	{ $$ = vn->OnBuildId($2, @2, $4, @4, $6); }
			| BUILDER_ID type_id_discrim_part VN_SEP VN_SEP item CLOSE_VN 					{ $$ = vn->OnBuildId($2, @2, wstring(), @4, $5); }

type_node_name_part : start_node_name_scope resolved_type_name		{ $$ = $2.as_andata_block; (void)$1; }

type_id_discrim_part : start_id_discrim_scope resolved_type_name 	{ $$ = $2.as_andata_block; (void)$1; }

arguments	: arguments COMMA norm_expression								{ $$ = $1; $$.push_back($3); }
			| norm_expression												{ $$.push_back($1); }

normal				: bare_statement

bare_statement		: norm_expression
					| SPACE_SEP_STMT_KEYWORD norm_expression				{ $$ = vn->OnSpaceSepStmtKeyword($1.as_ascii, @1, $2, @2); }
					| norm_expression ':'									{ $$ = vn->OnLabel($1, @1); } 
                                    
norm_expression 	: norm_id_value

norm_id_value		: norm_disjunction
					| norm_id_value VN_COLON norm_disjunction					{ $$ = vn->OnIdValuePair($1, @1, $3); }
					
norm_disjunction	: norm_conjunction
					| norm_disjunction DISJUNCTION norm_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

norm_conjunction 	: norm_assignment
					| norm_conjunction CONJUNCTION norm_assignment				{ $$ = vn->OnConjunction($1, $3); }
		
norm_assignment 	: norm_logical_or 
					| 				   norm_logical_or C_ASSIGN norm_assignment	{ $$ = vn->OnInfixOperator($2, $1, $3); }
					|         norm_logical_or C_COMPOUND_ASSIGN norm_assignment	{ $$ = vn->OnInfixOperator($2, $1, $3); }
					|   norm_logical_or '?' norm_expression ':' norm_assignment	{ $$ = vn->OnConditionalOperator($1, $3, $5); }

norm_logical_or 	: norm_logical_and
					| norm_logical_or C_LOGICAL_OR_OP norm_logical_and			{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_logical_and	: norm_bitwise_or
					| norm_logical_and C_LOGICAL_AND_OP norm_bitwise_or			{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_bitwise_or 	: norm_bitwise_xor
					| norm_bitwise_or C_BITWISE_OR_OP norm_bitwise_xor			{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_bitwise_xor	: norm_bitwise_and
					| norm_bitwise_xor C_BITWISE_XOR_OP norm_bitwise_and		{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_bitwise_and	: norm_equality
					| norm_bitwise_and C_AMPERSAND norm_equality				{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_equality 		: norm_relational
					| norm_equality C_EQUALITY_OP norm_relational				{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_relational 	: norm_shift
					| norm_relational C_RELATIONAL_OP norm_shift				{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_shift			: norm_add_etc
					| norm_shift C_SHIFT_OP norm_add_etc						{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_add_etc 		: norm_mul_etc
					| norm_add_etc C_ADD_SUB_OP norm_mul_etc					{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_mul_etc 		: norm_prefix
					| norm_mul_etc C_DIV_MOD_OP norm_prefix						{ $$ = vn->OnInfixOperator($2, $1, $3); }
					| norm_mul_etc '*' norm_prefix								{ $$ = vn->OnInfixOperator("*", $1, $3); }
		
norm_prefix : norm_postfix
			| 			       DELTA normal DELTA_ARROW norm_prefix	 { $$ = vn->OnDelta($2, $4); }
			| 					    C_PREFIX_OP 		norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_AMPERSAND 		norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_STAR		 		norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_ADD_SUB_OP	    norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_PRE_OR_INFIX_OP   norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_PRE_OR_POSTFIX_OP norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 									  GRASS norm_prefix	 { $$ = vn->OnGrass($2); }
			| 								 POINTER_IS norm_prefix	 { $$ = vn->OnPointerIs($2); }
			| 								   NEGATION norm_prefix	 { $$ = vn->OnNegation($2); }
			|       TRANSFORM transform_norm_part CLOSE_VN type_prefix	{ $$ = vn->OnTransform($2, @2, $4, @4); }
			| 								 BUILD_SIZE unified_prefix	{ $$ = vn->OnBuildSize($2); }
			| 								  STRINGIZE unified_prefix	{ $$ = vn->OnStringize($2); }
			|  									  STUFF unified_prefix	{ $$ = vn->OnStuff($2, nullptr, VN::Limit()); }
			|  STUFF OPEN_VN           unified CLOSE_VN unified_prefix	{ $$ = vn->OnStuff($5, $3, VN::Limit()); }
			|  STUFF OPEN_VN stuff_depth_limit CLOSE_VN unified_prefix	{ $$ = vn->OnStuff($5, nullptr, $3); }
			|       PRERESTRICT node_name_part CLOSE_VN unified_prefix	{ $$ = vn->OnRestrict($2, @2, $4, @4); }

transform_norm_part : start_transform_name_scope TRANSFORM_NAME_NORMAL { $$ = $2.as_ascii; (void)$1; }

// Use a prerestriction to disambiguate ‚§®„ÄêDeclarationOf„Äë...
unified_prefix	: norm_prefix
				| type_prefix
				| TRANSFORM transform_unif_part CLOSE_VN norm_prefix	 { $$ = vn->OnTransform($2, @2, $4, @4); }
  
transform_unif_part : start_transform_name_scope TRANSFORM_NAME_UNIFIED { $$ = $2.as_ascii; (void)$1; }
  
norm_postfix: norm_paren     
			| norm_postfix C_PRE_OR_POSTFIX_OP 				 		{ $$ = vn->OnPostfixOperator($2, $1); }
			| norm_postfix '[' normal ']'	 				 		{ $$ = vn->OnSubscript($1, $3); }
			| norm_postfix OPEN_MAP_CALL arguments CLOSE_MAP_CALL	{ $$ = vn->OnMapArgsCall($1, $3); }
			| norm_postfix OPEN_MAP_CALL CLOSE_MAP_CALL				{ $$ = vn->OnMapArgsCall($1, list<TreePtr<Node>>()); }
			| norm_postfix '(' arguments ')'		 				{ $$ = vn->OnSeqArgsCall($1, $3); }
			| norm_postfix '(' ')'	 								{ $$ = vn->OnSeqArgsCall($1, list<TreePtr<Node>>()); }
			| norm_postfix '.' norm_unit							{ $$ = vn->OnLookup($1, $3, @3); }
		
norm_paren	: norm_unit
			| '(' commands ')'                          		{ $$ = vn->OnEmbeddedCommands($2); }

norm_unit	: BUILT_IN node_name_part close_itemisation			{ $$ = vn->OnBuiltIn($2, @2, $3); }
			| STAR												{ $$ = vn->OnStar(nullptr); }
			| STAR OPEN_VN normal CLOSE_VN						{ $$ = vn->OnStar($3); }
			| INTEGRAL_LITERAL					      			{ $$ = vn->OnIntegralLiteral($1, @1); }
			| STRING_LITERAL					      			{ $$ = vn->OnStringLiteral($1); }
			| BOOL_LITERAL										{ $$ = vn->OnBoolLiteral($1); }
			| ANY												{ $$ = nullptr; }
			| NORM_TERM_KEYWORD									{ $$ = vn->OnNormalTerminalKeyword($1.as_ascii, @1); }
			| '{' '}'                          					{ $$ = vn->OnCompound({}); } 
			| '{' bare_statements ';' '}'                  		{ $$ = vn->OnCompound($2); }
			| C_ASSIGN '{' '}'             				  		{ $$ = vn->OnArrayLiteral(VN::Item()); } // ={...} dismbiguates against compound
			| C_ASSIGN '{' item '}'         		  	  		{ $$ = vn->OnArrayLiteral($3); }               
			| DESIGNATED_NORMAL       				  			{ $$ = $1.as_designated; }
			| SPECIFIC_ID id_discrim_part VN_SEP STRING_LITERAL CLOSE_VN			{ $$ = vn->OnSpecificId($2, @2, $4, @4); }
			| SPECIFIC_ID id_discrim_part VN_SEP CLOSE_VN							{ $$ = vn->OnSpecificId($2, @2, wstring(), @4); }
			| ID_BY_NAME id_discrim_part VN_SEP STRING_LITERAL CLOSE_VN				{ $$ = vn->OnIdByName($2, @2, $4, @4); } // ASCII_NAME could be format
			| BUILDER_ID id_discrim_part VN_SEP STRING_LITERAL VN_SEP CLOSE_VN 		{ $$ = vn->OnBuildId($2, @2, $4, @4, VN::Item{}); }
			| BUILDER_ID id_discrim_part VN_SEP STRING_LITERAL VN_SEP item CLOSE_VN	{ $$ = vn->OnBuildId($2, @2, $4, @4, $6); }
			| BUILDER_ID id_discrim_part VN_SEP VN_SEP item CLOSE_VN 				{ $$ = vn->OnBuildId($2, @2, wstring(), @4, $5); }

node_name_part : start_node_name_scope resolved_name	{ $$ = $2.as_andata_block; (void)$1; }

id_discrim_part : start_id_discrim_scope resolved_name	{ $$ = $2.as_andata_block; (void)$1; }

vn_name		: UNICODE_NAME    							{ $$ = $1.as_unicode; }
			| ASCII_NAME								{ $$ = $1.as_unicode; }     
          
// RESOLVED_X is strong i.e. there was definitely a ::. resolved_x is weak and
// doesn't require this. See comment by part_resolve for why we don't need a value from 
// part_resolve. The Gnomon is still around because we *could* use $1 here, so it must be valid.
resolved_name	: part_resolve RESOLVED_NORMAL			{ $$ = $2; }
				| RESOLVED_NORMAL						{ $$ = $1; }

resolved_type_name	: part_resolve RESOLVED_TYPE		{ $$ = $2; }
					| RESOLVED_TYPE						{ $$ = $1; }

// On sight of ::, we inform the recognition layer, which returns a shared_ptr which will 
// expire when the production is complete or an error forces unwind. The naming layer
// keeps weak_ptrs to these and will send us RESOLVED_NORMAL if one exists.
part_resolve	: node_namespace RESOLUTION_OP			{ $$ = make_shared<VN::ResolverGnomon>($1.as_andata_block);
                                                          recogniser->AddGnomon($$); }
    
node_namespace 	: part_resolve NODE_NAMESPACE			{ $$ = $2; }
				| NODE_NAMESPACE						{ $$ = $1; }				      

start_node_name_scope	: OPEN_VN 			{ $$ = make_shared<VN::NodeNameScopeGnomon>(); 
										      recogniser->AddGnomon($$); }

start_id_discrim_scope	: OPEN_VN 			{ $$ = make_shared<VN::IdentifierDiscriminatorScopeGnomon>(); 
											  recogniser->AddGnomon($$); }

start_transform_name_scope	: OPEN_VN 		{ $$ = make_shared<VN::TransformNameScopeGnomon>(); 
											  recogniser->AddGnomon($$); }

%%
