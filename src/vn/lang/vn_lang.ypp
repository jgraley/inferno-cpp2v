%require "3.2"

%language "c++"
%define api.namespace {YY}
%define api.parser.class {VNLangParser}
%define api.value.type variant
%define api.token.constructor
%define parse.error detailed
// Inadequacy Elimination LR
%define lr.type ielr
// Lookahead Correction
%define parse.lac full

%defines

%locations
%define api.location.file "vn_lang.location.hpp"

%code requires {
  // Appears high-ish in the generated .hpp file
  #include "vn_parse.hpp" // TODO we actually only need Item and Itemisation from here
  #include "node/tree_ptr.hpp"
  #include "tree/node_names.hpp"
  #include <string>
  namespace YY {
    class VNLangScanner;  
  	struct NameInfo
	{
		TreePtr<Node> as_designated;
		string as_ascii;
		wstring as_unicode;
		const AvailableNodeData::Block *as_andata_block;
	};     	
  };
  // Things we point to from semantic values
  namespace VN { 
    class Command;
    class Gnomon;
    class ScopeBlockGnomon;
  }
}

%code { 
  // Appears high-ish in the generated .cpp file
  #include "vn_lang.lpp.hpp"  /* Generated by reflex. */
  #include <iostream>
  #include <fstream>
  #include "vn_commands.hpp"
  #undef yylex
  #define yylex lexer.lex  /* Within bison's parse() we should invoke lexer.lex(), not the global yylex() */
  
  // Strings that could be translated have _() around them. 
  // See https://www.gnu.org/software/gettext/manual/gettext.html
  #define _(String) (String)
  #define N_(String) String 
}

%parse-param { YY::VNLangScanner& lexer }
%parse-param { VN::VNParse *vn }
%parse-param { VN::VNLangRecogniser *recogniser }

%define api.token.prefix {TOK_}

%token
	DESIGNATE 								"‚™Æ"
	VN_SEMI 								"‚®ü"
	VN_SEP 									"‚ö¨"
	'('
	')'
	';'
	SCOPE_RES 								"::"
	ENGINE	 								"Í©ú"
	STUFF		 							"‚©®"
	DELTA 									"‚ñ≤"
	DELTA_ARROW 							"‚Øà"
	BUILT_IN 								"‚óº"
	BUILT_IN_TYPE							"‚ØÅ"
	OPEN_VN 								"„Äê"
	CLOSE_VN 								"„Äë"
	ANY 									"‚òÜ"
	STAR 									"‚òÖ"
	ID_BY_NAME								"‚äõ"
	SPECIFIC_ID								"üûä"
	BUILD_ID								"‚ßá"
	OPEN_CHEVRON							"„Ää"
	CLOSE_CHEVRON							"„Äã"
	TRANSFORM								"‚§®"
	NEGATION								"¬¨"
	CONJUNCTION								"‚àß"
	DISJUNCTION								"‚à®"
	GRASS									"‚´¥"
	POINTER_IS								"‚Æé"
	BUILD_SIZE								"üÑΩ"
	STRINGIZE								"¬ß"
	PRERESTRICT								"‚ÄΩ"
	OPEN_TYPE								"‚¶Ö"
	CLOSE_TYPE								"‚¶Ü"
	<string>	COMMA						_("comma")
	<string>	C_ASSIGN					"="
	<string>	C_COMPOUND_ASSIGN			_("C compound assignment operator")
	<string>	C_LOGICAL_OR_OP				"||"			
	<string>	C_LOGICAL_AND_OP			"&&"			
	<string>	C_BITWISE_OR_OP				"|"
	<string>	C_BITWISE_XOR_OP			"^"
	<string>	C_AMPERSAND					"&"
	<string>	C_EQUALITY_OP				_("C equality operator")
	<string>  	C_RELATIONAL_OP				_("C relation operator")
	<string>	C_SHIFT_OP					_("C shift operator")
	<string>	C_ADD_SUB_OP				_("C add/subtract")
	<string>    C_DIV_MOD_OP				_("C divide/modulo")
	<string> 	C_STAR						"*"
	<string>  	C_PREFIX_OP					_("C prefix operator")
	<string>	C_PRE_OR_INFIX_OP			_("C prefix/infix operator")
	<string>    C_PRE_OR_POSTFIX_OP 		_("C prefix/postfix operator")
	<wstring> 	STRING_LITERAL 				_("string")
	<string> 	INTEGRAL_LITERAL			_("integer literal")
	<bool>		BOOL_LITERAL				_("boolean literal")
	<YY::NameInfo>  ASCII_NAME 				_("ASCII name")
	<string>  		ASCII_FORMAT 			_("ASCII format string")
	<YY::NameInfo> 	UNICODE_NAME 			_("Unicode name")
	<string>  		TYPENAME				_("typename")
	<YY::NameInfo>	DESIGNATED_NONTYPE		_("designated non-type")
	<YY::NameInfo>	DESIGNATED_TYPE			_("designated type")
	<YY::NameInfo>	RESOLVED_NONTYPE		_("resolved non-type node name")
	<YY::NameInfo>	RESOLVED_TYPE			_("resolved type node name")
	<YY::NameInfo>	SCOPE_NAME				_("scope name")
	
%type <TreePtr<Node>> 				normal norm_unit norm_postfix norm_prefix norm_relational norm_disjunction norm_conjunction  
%type <TreePtr<Node>> 				norm_assignment norm_equality norm_add_etc norm_logical_or norm_logical_and statements statement
%type <TreePtr<Node>> 				norm_mul_etc norm_shift norm_bitwise_and norm_bitwise_or norm_bitwise_xor norm_no_comma_op 
%type <TreePtr<Node>> 				norm_paren norm_comma_op 
%type <TreePtr<Node>> 				type type_unit type_prefix type_disjunction type_conjunction type_paren
%type <TreePtr<Node>> 				unified_no_comma_op unified unified_prefix
%type <wstring> 					vn_name
%type <YY::NameInfo> 				resolved_name resolved_type_name resolve
%type <list<shared_ptr<VN::Command>>>			commands sep_commands
%type <shared_ptr<VN::Command>>		script_command 
%type <VN::Limit>					limit
%type <VN::Itemisation>				itemisation items_bare items_sep
%type <VN::Item>					item
%type <shared_ptr<VN::Gnomon>>		part_resolve recogniser_command 
%%

vnscript 		: commands 								{ vn->OnVNScript($1); }
     
// This is to make the ‚®ü optional on last command
commands        : sep_commands							{ $$ = $1; }
				| sep_commands VN_SEMI					{ $$ = $1; }
				
// The ‚®ü turns patterns into commands. This rule must 
// be left-associative so calls to OnParse() happen
// in the correct order. STOP BREAKING THIS!!!
sep_commands 	: sep_commands VN_SEMI script_command		{ $$ = $1; $$.push_back($3); }	
				| script_command							{ $$.push_back($1); }
				| sep_commands VN_SEMI recogniser_command	{ $$ = $1; recogniser->AddGnomon($3); }	
				| recogniser_command						{ recogniser->AddGnomon($1); }
			
// Add more commands here
script_command	: ENGINE normal   		 		 	{ $$ = make_shared<VN::EngineCommand>($2, @1); }
				| normal 			  				{ $$ = make_shared<VN::PatternCommand>($1, @$); }

// or here
recogniser_command	: vn_name DESIGNATE normal		{ $$ = make_shared<VN::NonTypeDesignationGnomon>($1, $3); }
					| vn_name DESIGNATE type			{ $$ = make_shared<VN::TypeDesignationGnomon>($1, $3); }


// This one dips into ASCII/C because VN doesn't otherwise need equality/inequality operators
limit		: C_ASSIGN INTEGRAL_LITERAL					{ $$ = VN::Limit{ $1, @1, $2, @2 }; }

itemisation : '(' items_bare ')'    					{ $$ = $2; $$.loc = @$; }
			| '(' items_sep ')'	    					{ $$ = $2; $$.loc = @$; $$.items.push_back({{}, @3}); }
			| '(' ')'									{ $$.loc = @$; $$.items.push_back({{}, @2}); }
  
items_bare 	: items_sep item							{ $$ = $1; $$.items.push_back($2); }
			| item										{ $$.items.push_back($1); }

items_sep 	: items_bare VN_SEP		   					{ $$ = $1; } 
			| items_sep VN_SEP		 					{ $$ = $1; $$.items.push_back({{}, @2}); }
			| VN_SEP	 								{ $$.items.push_front({{}, @1}); }

item		: item COMMA unified_no_comma_op			{ $$ = $1; $$.nodes.push_back($3); $$.loc = @$; }
			| unified_no_comma_op						{ $$.nodes.push_back($1); $$.loc = @$; }

unified_no_comma_op : norm_no_comma_op					{ $$ = $1; }
					| type								{ $$ = $1; }

unified		: normal									{ $$ = $1; }
			| type										{ $$ = $1; }

statements	: statement

statement	: normal
          
type		: type_disjunction          
          
type_disjunction	: type_conjunction
					| type_disjunction DISJUNCTION type_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

type_conjunction 	: type_prefix
					| type_conjunction CONJUNCTION type_prefix				{ $$ = vn->OnConjunction($1, $3); }
                    
type_prefix : 										     type_paren
			| 				      DELTA type DELTA_ARROW type_prefix { $$ = vn->OnDelta($2, $4); }
			| PRERESTRICT OPEN_VN resolved_type_name CLOSE_VN type_prefix { $$ = vn->OnRestrict($3.as_andata_block, @3, $5, @5); }
			| 									   GRASS type_prefix { $$ = vn->OnGrass($2); }
			| 								  POINTER_IS type_prefix { $$ = vn->OnPointerIs($2); }
			| 								    NEGATION type_prefix { $$ = vn->OnNegation($2); }

type_paren  : type_unit
			| OPEN_TYPE unified CLOSE_TYPE                  { $$ = $2; }	
			
                                        
type_unit	: BUILT_IN_TYPE resolved_type_name itemisation	{ $$ = vn->OnBuiltIn($2.as_andata_block, @2, $3); }
			| BUILT_IN_TYPE resolved_type_name				{ $$ = vn->OnBuiltIn($2.as_andata_block, @2, VN::Itemisation{{}, @2}); }
			| BUILT_IN resolved_type_name itemisation		{ $$ = vn->OnBuiltIn($2.as_andata_block, @2, $3); }
			| BUILT_IN resolved_type_name					{ $$ = vn->OnBuiltIn($2.as_andata_block, @2, VN::Itemisation{{}, @2}); }
			| DESIGNATED_TYPE       				 		{ $$ = $1.as_designated; }		
   			| SPECIFIC_ID OPEN_VN resolved_type_name VN_SEP STRING_LITERAL CLOSE_VN				{ $$ = vn->OnSpecificId($3.as_andata_block, @3, $5, @5); }
			| SPECIFIC_ID OPEN_VN resolved_type_name VN_SEP CLOSE_VN							{ $$ = vn->OnSpecificId($3.as_andata_block, @3, wstring(), @5); }
			| ID_BY_NAME OPEN_VN resolved_type_name VN_SEP STRING_LITERAL CLOSE_VN				{ $$ = vn->OnIdByName($3.as_andata_block, @3, $5, @5); } // ASCII_NAME could be format
			| BUILD_ID OPEN_VN resolved_type_name VN_SEP STRING_LITERAL VN_SEP CLOSE_VN 		{ $$ = vn->OnBuildId($3.as_andata_block, @3, $5, @5, VN::Item{}); }
			| BUILD_ID OPEN_VN resolved_type_name VN_SEP STRING_LITERAL VN_SEP item CLOSE_VN	{ $$ = vn->OnBuildId($3.as_andata_block, @3, $5, @5, $7); }
			| BUILD_ID OPEN_VN resolved_type_name VN_SEP VN_SEP item CLOSE_VN 					{ $$ = vn->OnBuildId($3.as_andata_block, @3, wstring(), @5, $6); }

normal				: norm_comma_op
                      
norm_comma_op		: norm_no_comma_op
					| norm_no_comma_op COMMA norm_comma_op						{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_no_comma_op 	: norm_disjunction

norm_disjunction	: norm_conjunction
					| norm_disjunction DISJUNCTION norm_conjunction				{ $$ = vn->OnDisjunction($1, $3); }

norm_conjunction 	: norm_assignment
					| norm_conjunction CONJUNCTION norm_assignment				{ $$ = vn->OnConjunction($1, $3); }
		
norm_assignment 	: 											norm_logical_or 
					| 				   norm_logical_or C_ASSIGN norm_assignment	{ $$ = vn->OnInfixOperator($2, $1, $3); }
					|         norm_logical_or C_COMPOUND_ASSIGN norm_assignment	{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_logical_or 	: norm_logical_and
					| norm_logical_or C_LOGICAL_OR_OP norm_logical_and			{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_logical_and	: norm_bitwise_or
					| norm_logical_and C_LOGICAL_AND_OP norm_bitwise_or			{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_bitwise_or 	: norm_bitwise_xor
					| norm_bitwise_or C_BITWISE_OR_OP norm_bitwise_xor			{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_bitwise_xor	: norm_bitwise_and
					| norm_bitwise_xor C_BITWISE_XOR_OP norm_bitwise_and		{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_bitwise_and	: norm_equality
					| norm_bitwise_and C_AMPERSAND norm_equality				{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_equality 		: norm_relational
					| norm_equality C_EQUALITY_OP norm_relational				{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_relational 	: norm_shift
					| norm_relational C_RELATIONAL_OP norm_shift				{ $$ = vn->OnInfixOperator($2, $1, $3); }

norm_shift			: norm_add_etc
					| norm_shift C_SHIFT_OP norm_add_etc						{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_add_etc 		: norm_mul_etc
					| norm_add_etc C_ADD_SUB_OP norm_mul_etc					{ $$ = vn->OnInfixOperator($2, $1, $3); }
		
norm_mul_etc 		: norm_prefix
					| norm_mul_etc C_DIV_MOD_OP norm_prefix						{ $$ = vn->OnInfixOperator($2, $1, $3); }
					| norm_mul_etc '*' norm_prefix								{ $$ = vn->OnInfixOperator("*", $1, $3); }
		
norm_prefix : 										   norm_postfix
			| 			   DELTA normal DELTA_ARROW norm_prefix	 { $$ = vn->OnDelta($2, $4); }
			| 					    C_PREFIX_OP 		norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_AMPERSAND 		norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_STAR		 		norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_ADD_SUB_OP	    norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_PRE_OR_INFIX_OP   norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			| 					    C_PRE_OR_POSTFIX_OP norm_prefix	 { $$ = vn->OnPrefixOperator($1, $2); }
			|							   '(' type ')' norm_prefix  { $$ = vn->OnCast($2, @2, $4, @4); }
			| PRERESTRICT OPEN_VN resolved_name CLOSE_VN norm_prefix { $$ = vn->OnRestrict($3.as_andata_block, @3, $5, @5); }
			| 									  GRASS norm_prefix	 { $$ = vn->OnGrass($2); }
			| 								 POINTER_IS norm_prefix	 { $$ = vn->OnPointerIs($2); }
			| 								   NEGATION norm_prefix	 { $$ = vn->OnNegation($2); }
			| 								 BUILD_SIZE unified_prefix	 { $$ = vn->OnBuildSize($2); }
			| 								  STRINGIZE unified_prefix	 { $$ = vn->OnStringize($2); }
			| 	  TRANSFORM OPEN_VN ASCII_NAME CLOSE_VN unified_prefix	 { $$ = vn->OnTransform($3.as_ascii, @3, $5, @5); }
			|  									  STUFF unified_prefix	 { $$ = vn->OnStuff($2, nullptr, VN::Limit()); }
			| 		  	 STUFF OPEN_VN unified CLOSE_VN unified_prefix	 { $$ = vn->OnStuff($5, $3, VN::Limit()); }
			| 			 STUFF OPEN_VN limit   CLOSE_VN unified_prefix	 { $$ = vn->OnStuff($5, nullptr, $3); }

unified_prefix	: norm_prefix
				| type_prefix

norm_postfix: norm_paren     
			| norm_postfix C_PRE_OR_POSTFIX_OP 				 	{ $$ = vn->OnPostfixOperator($2, $1); }
		
norm_paren	: norm_unit
			| '(' commands ')'                          		{ $$ = vn->OnEmbeddedCommands($2); }

norm_unit	: BUILT_IN resolved_name itemisation				{ $$ = vn->OnBuiltIn($2.as_andata_block, @2, $3); }
			| BUILT_IN resolved_name							{ $$ = vn->OnBuiltIn($2.as_andata_block, @2, VN::Itemisation{{}, @2}); }
			| STAR												{ $$ = vn->OnStar(nullptr); }
			| STAR OPEN_VN normal CLOSE_VN						{ $$ = vn->OnStar($3); }
			| INTEGRAL_LITERAL					      			{ $$ = vn->OnIntegralLiteral($1, @1); }
			| STRING_LITERAL					      			{ $$ = vn->OnStringLiteral($1); }
			| BOOL_LITERAL										{ $$ = vn->OnBoolLiteral($1); }
			| ANY												{ $$ = nullptr; }
			| '{' statements '}'                          		{ $$ = $2; }
			| DESIGNATED_NONTYPE       				  			{ $$ = $1.as_designated; }
			| SPECIFIC_ID OPEN_VN resolved_name VN_SEP STRING_LITERAL CLOSE_VN			{ $$ = vn->OnSpecificId($3.as_andata_block, @3, $5, @5); }
			| SPECIFIC_ID OPEN_VN resolved_name VN_SEP CLOSE_VN							{ $$ = vn->OnSpecificId($3.as_andata_block, @3, wstring(), @5); }
			| ID_BY_NAME OPEN_VN resolved_name VN_SEP STRING_LITERAL CLOSE_VN			{ $$ = vn->OnIdByName($3.as_andata_block, @3, $5, @5); } // ASCII_NAME could be format
			| BUILD_ID OPEN_VN resolved_name VN_SEP STRING_LITERAL VN_SEP CLOSE_VN 		{ $$ = vn->OnBuildId($3.as_andata_block, @3, $5, @5, VN::Item{}); }
			| BUILD_ID OPEN_VN resolved_name VN_SEP STRING_LITERAL VN_SEP item CLOSE_VN	{ $$ = vn->OnBuildId($3.as_andata_block, @3, $5, @5, $7); }
			| BUILD_ID OPEN_VN resolved_name VN_SEP VN_SEP item CLOSE_VN 				{ $$ = vn->OnBuildId($3.as_andata_block, @3, wstring(), @5, $6); }

vn_name		: UNICODE_NAME    							{ $$ = $1.as_unicode; }
			| ASCII_NAME								{ $$ = $1.as_unicode; }     
          
// On sight of ::, we inform the recognition layer, which returns a shared_ptr which will 
// expire when the production is complete or an error forces unwind. The naming layer
// keeps weak_ptrs to these and will send us RESOLVED_NONTYPE if one exists.
part_resolve	: resolve SCOPE_RES						{ $$ = make_shared<VN::ScopeBlockGnomon>($1.as_andata_block);
                                                          recogniser->AddGnomon($$); }
    
resolve     	: part_resolve SCOPE_NAME				{ $$ = $2; }
				| SCOPE_NAME							{ $$ = $1; }
				      

// RESOLVED_NONTYPE is strong i.e. there was definitely a ::. resolved_name is weak and
// doesn't require this. See comment by part_resolve for why we don't need a value from it.
resolved_name	: part_resolve RESOLVED_NONTYPE			{ $$ = $2; }
				| RESOLVED_NONTYPE						{ $$ = $1; }

resolved_type_name	: part_resolve RESOLVED_TYPE		{ $$ = $2; }
					| RESOLVED_TYPE						{ $$ = $1; }

%%
